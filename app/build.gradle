plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'

    // ❗作为 Xposed 模块使用务必添加，其它情况可选
    id 'com.google.devtools.ksp' version '1.8.21-1.0.11'
}

android {
    signingConfigs {
        apply from: 'sign.gradle'
        def keyFile = "${storeFile}"
        def keyPwd = "${storePassword}"
        def alias = "${keyAlias}"
        def aliasPwd = "${keyPassword}"
        release {
            storeFile file(keyFile)
            storePassword keyPwd
            keyAlias alias
            keyPassword aliasPwd
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            // 显示app名称+当前版本号
            def fileName = "MiuiFreeFormX_${variant.versionName}"
            // 如果是debug版本，加入beta版本号
            if (isRelease()) {
                // 如果是release版本，直接显示
                fileName = fileName + "_release.apk"
            } else {
                fileName = fileName + "_debug.apk"
            }
            outputFileName = fileName
        }
    }
    namespace 'org.liuyi.mifreeformx'
    compileSdk 33

    defaultConfig {
        applicationId "org.liuyi.mifreeformx"
        minSdk 30
        targetSdk 33
        versionCode getSelfDefinedVersion("code")
        versionName getSelfDefinedVersion("name")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {

    implementation project(path: ':blockmiui')
    implementation 'androidx.annotation:annotation-jvm:1.6.0'
    implementation 'androidx.collection:collection:1.2.0'

    // 基础依赖
    implementation 'com.highcapable.yukihookapi:api:1.1.11'
    // ❗作为 Xposed 模块使用务必添加，其它情况可选
    compileOnly 'de.robv.android.xposed:api:82'
    // ❗作为 Xposed 模块使用务必添加，其它情况可选
    ksp 'com.highcapable.yukihookapi:ksp-xposed:1.1.11'

    implementation 'com.blankj:utilcodex:1.31.1'
}

boolean isRelease() {
    for (String taskName : gradle.startParameter.taskNames) {
        if (taskName.contains("Release") | taskName.contains("release")) {
            return true
        }
    }
    return false
}

def getSelfDefinedVersion(type) {
    int aa = 1
    int bb = 0  // 0-99
    int cc = 8  // 0-99

    if ("code" == type) {
        aa * 10000 + bb * 100 + cc
    } else if ("name" == type) {
        if (isRelease()) {
            String today = new Date().format("yy.MM.dd.HH")
            "V$today"
        } else {
            String today = new Date().format("yy.MM.dd.HHmmss")
            "$today"
        }
    }
}